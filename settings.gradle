pluginManagement {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        maven { url = "https://maven.minecraftforge.net/" }
    }
}

dependencyResolutionManagement {
    versionCatalogs {
        // Mod Dependencies
        mods {
            version("jei", "15.20.0.105")
            version("rei", "12.1.785")
            version("emi", "1.1.13+${minecraft_version}")
            version("jade", "11.6.3")
            version("mixinextras", "0.2.0")

            library("jei-common-api", "mezz.jei", "jei-${minecraft_version}-common-api").versionRef("jei")
            library("jei-forge-api", "mezz.jei", "jei-${minecraft_version}-forge-api").versionRef("jei")
            library("jei-forge-impl", "mezz.jei", "jei-${minecraft_version}-forge").versionRef("jei")

            library("rei-plugin", "me.shedaniel", "RoughlyEnoughItems-default-plugin-forge").versionRef("rei")
            library("rei-api", "me.shedaniel", "RoughlyEnoughItems-api-forge").versionRef("rei")
            library("rei-forge", "me.shedaniel", "RoughlyEnoughItems-forge").versionRef("rei")

            library("emi", "dev.emi", "emi-forge").versionRef("emi")
            library("jade", "maven.modrinth", "jade").versionRef("jade")

            library("mixinextras-common", "io.github.llamalad7", "mixinextras-common").versionRef("mixinextras")
            library("mixinextras-forge", "io.github.llamalad7", "mixinextras-forge").versionRef("mixinextras")
        }

        libs {
            version("modDevGradleLegacy", "2.0.52-beta")
            version("machete", "2.0.1")
            version("lombok", "8.7.1")

            version("jetbrains-annotations", "24.1.0")

            plugin("modDevGradleLegacy", "net.neoforged.moddev.legacyforge").versionRef("modDevGradleLegacy")
            plugin("machete", "io.github.p03w.machete").versionRef("machete")
            plugin("lombok", "io.freefair.lombok").versionRef("lombok")

            library("jetbrains-annotations", "org.jetbrains", "annotations").versionRef("jetbrains-annotations")
        }
    }
}

rootProject.name = mod_id