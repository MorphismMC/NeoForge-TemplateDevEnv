plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    alias libs.plugins.modDevGradle
    alias libs.plugins.machete // automatic jar compressing on build
    alias libs.plugins.lombok
}

version = mod_version
group = mod_group_id

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

apply from: "$rootDir/gradle/scripts/moddevgradle.gradle"
apply from: "$rootDir/gradle/scripts/repositories.gradle"
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/gradle/scripts/resources.gradle"
apply from: "$rootDir/gradle/scripts/jars.gradle"
apply from: "$rootDir/gradle/scripts/publishing.gradle"

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

machete {
    // disable machete locally for faster builds
    enabled = false
}

lombok {
    version = "1.18.32"
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

if (use_render_doc) {
    tasks.register("runWithRenderDoc", Exec) {
        def javaExecTask = tasks.withType(JavaExec).named("runClient").get()
        def javaHome = javaExecTask.javaLauncher.get().metadata.installationPath.asFile.absolutePath
        commandLine = [
                "${render_doc_path}\\renderdoccmd.exe",
                "capture",
                "--opt-hook-children",
                "--wait-for-exit",
                "--working-dir",
                ".",
                "$javaHome/bin/java.exe",
                "-Xmx64m",
                "-Xms64m",
                "-Dorg.gradle.appname=gradlew",
                "-Dorg.gradle.java.home=$javaHome",
                "-classpath",
                "gradle/wrapper/gradle-wrapper.jar",
                "org.gradle.wrapper.GradleWrapperMain",
                "runClient"
        ]
    }
}